(define (domain libros)

    (:requirements :strips :adl :typing :equality :fluents) 

    (:types
        libro - object
        mes - object
    )

    (:predicates
        (predecesor ?l1 - libro ?l2 - libro) ;primero el l1 y despues l2
        (paralelo ?l1 - libro ?l2 - libro)
        (leido ?l1 - libro)
        (quiere ?l1 - libro)
        (asignado ?l1 - libro ?m1 - mes)
        (mes-anterior ?m1 - mes ?m2 - mes)      ;m2 anterior a m1
        (mes-anterior-p ?m1 - mes ?m2 - mes)    ;m1 anterior a m2
    )

    (:functions
        (pag-por-mes ?m - mes)
        (pag-libro ?l - libro)
    )

    (:action leer-pr
        :parameters (?libro - libro ?mes - mes)
        :precondition (and 
                        (< (pag-por-mes ?mes) 800)
                        (not (leido ?libro))
                        (forall (?pred - libro) 
                            (imply (predecesor ?pred ?libro) (and (leido ?pred)
                                (forall (?mes-anterior - mes) 
                                    (imply (asignado ?pred ?mes-anterior) 
                                        (mes-anterior ?mes ?mes-anterior)))
                            )))
                        ;(not (exists (?para - libro) (paralelo ?para ?libro)))
                        )
        :effect (and 
                    (leido ?libro)
                    (not (quiere ?libro))
                    (asignado ?libro ?mes)
                    (increase (pag-por-mes ?mes) (pag-libro ?libro))
                )
    )

    (:action leer-pa
        :parameters (?libro - libro ?mes - mes)
        :precondition (and 
                        (< (pag-por-mes ?mes) 800)
                        (not (leido ?libro))
                        (not (exists (?pred - libro) (predecesor ?pred ?libro)))
                        (forall (?par - libro)
                            (imply (or (paralelo ?libro ?par) (paralelo ?par ?libro))
                                (and (leido ?par) (forall (?mes-p - mes)
                                    (imply (asignado ?par ?mes-p) (or 
                                        (mes-anterior-p ?mes-p ?mes)
                                        (mes-anterior-p ?mes ?mes-p)
                                        (= ?mes ?mes-p))))))))
        :effect (and 
                    (leido ?libro)
                    (not (quiere ?libro))
                    (asignado ?libro ?mes)
                    (increase (pag-por-mes ?mes) (pag-libro ?libro))
                    )
    )

    (:action leer-si
        :parameters (?libro - libro ?mes - mes)
        :precondition (and 
                        (< (pag-por-mes ?mes) 800)
                        (quiere ?libro)
                        (not (leido ?libro))
                        (not (exists (?pred - libro) (predecesor ?pred ?libro)))
                        (not (exists (?para - libro) 
                            (or (paralelo ?libro ?para) (paralelo ?para ?libro))))
                        )
        :effect (and 
                    (leido ?libro)
                    (not (quiere ?libro))
                    (asignado ?libro ?mes)
                    (increase (pag-por-mes ?mes) (pag-libro ?libro))
                    )
    )
    
)
